open EmailStyles

let html_body ~handle ~new_email ~code : JSX.element =
  let destination_text =
    match new_email with
    | Some email ->
        <span style=Styles.link>(JSX.string (" to " ^ email))</span>
    | None ->
        JSX.string ""
  in
  <div>
    <p style=Styles.paragraph>
      (JSX.string "Hello, ")
      <span style=Styles.link>(JSX.string handle)</span>
      (JSX.string "!")
    </p>
    <p style=Styles.paragraph>
      (JSX.string
         "Please confirm that you would like to change your email address" )
      destination_text
      (JSX.string " using the token below:")
    </p>
    <div style=Styles.code_block>(JSX.string code)</div>
    <p style=Styles.small_text>
      (JSX.string "This token will expire in 10 minutes.")
    </p>
    <p style=Styles.small_text>
      (JSX.string
         "If you didn't request this email change, you can ignore this email." )
    </p>
  </div>

let plain_text ~handle ~new_email ~code : string =
  let destination_text =
    match new_email with Some email -> " to " ^ email | None -> ""
  in
  Printf.sprintf
    "Hello, %s!\n\n\
     Please confirm that you would like to change your email address%s using \
     the token below:\n\n\
     %s\n\n\
     This token will expire in 10 minutes.\n\n\
     If you didn't request this email change, you can ignore this email."
    handle destination_text code

let make ~handle ~new_email ~code : Letters.body =
  let html_content = html_body ~handle ~new_email ~code in
  let template_content =
    EmailTemplate.
      { title= "Confirm email change for " ^ handle
      ; heading= "confirm email change"
      ; body= html_content
      ; footer= Some ("This is an automated message from " ^ Env.hostname ^ ".")
      }
  in
  let html = EmailTemplate.make ~content:template_content () |> JSX.render in
  let plain = plain_text ~handle ~new_email ~code in
  Mixed (plain, html, None)
