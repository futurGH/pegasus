[@@@ocaml.warning "-26-27"]

open Melange_json.Primitives
open React

type migration_step =
  | EnterCredentials
  | ResumeAvailable
  | Enter2FA
  | ImportingData
  | EnterPlcToken
  | Complete
  | Error

let step_of_string = function
  | "enter_credentials" ->
      EnterCredentials
  | "resume_available" ->
      ResumeAvailable
  | "enter_2fa" ->
      Enter2FA
  | "importing_data" ->
      ImportingData
  | "enter_plc_token" ->
      EnterPlcToken
  | "complete" ->
      Complete
  | "error" ->
      Error
  | _ ->
      EnterCredentials

let step_to_label = function
  | EnterCredentials ->
      "enter credentials"
  | ResumeAvailable ->
      "resume migration"
  | Enter2FA ->
      "two-factor authentication"
  | ImportingData ->
      "importing data"
  | EnterPlcToken ->
      "confirm identity"
  | Complete ->
      "migration complete"
  | Error ->
      "error"

let step_to_number = function
  | EnterCredentials | ResumeAvailable | Enter2FA ->
      0
  | ImportingData ->
      1
  | EnterPlcToken ->
      2
  | Complete | Error ->
      3

let total_steps = 3

module ProgressIndicator = struct
  let[@react.component] make ~step_number ~label () =
    <div className="mb-6">
      <div className="flex items-center gap-1 text-sm text-mist-100 mb-2">
        <span>
          (string
             ( "step " ^ string_of_int step_number ^ " of "
             ^ string_of_int total_steps ^ ": " ) )
        </span>
        <span className="font-medium text-mana-100">(string label)</span>
      </div>
      <div
        className="w-full bg-mist-60 rounded-full h-3"
        style=(ReactDOM.Style.make
                 ~boxShadow:"0 4px 8px 0 rgba(115, 117, 121, 0.30) inset" () )>
        <div
          className="bg-mana-100 shadow-elixir h-3 rounded-full transition-all \
                     duration-300"
          style=(ReactDOM.Style.make
                   ~width:(string_of_int (step_number * 100 / total_steps) ^ "%")
                   () )
        />
      </div>
    </div>
end

module AlertMessage = struct
  let[@react.component] make ~message ~kind () =
    let bg_class, text_class, icon =
      match kind with
      | `Error ->
          ( "bg-phoenix-100/10 border-phoenix-100/30"
          , "text-phoenix-100"
          , <CircleAlertIcon className="w-4 h-4 mr-2 mt-0.5 flex-shrink-0" /> )
      | `Success ->
          ( "bg-mana-100/10 border-mana-100/30"
          , "text-mana-100"
          , <CheckmarkIcon className="w-4 h-4 mr-2 mt-0.5 flex-shrink-0" /> )
    in
    <div className=("mb-4 p-3 border rounded-lg " ^ bg_class)>
      <span className=("inline-flex items-start text-sm " ^ text_class)>
        icon <span>(string message)</span>
      </span>
    </div>
end

module LoadingOverlay = struct
  type loading_step =
    { label: string
    ; message: string
    ; duration: int (* ms before moving to next step *) }

  let steps =
    [| { label= "authenticating"
       ; message= "Logging into your current PDS..."
       ; duration= 2000 (* fake step *) }
     ; { label= "creating account"
       ; message= "Creating your account on this PDS..."
       ; duration= 2000 (* fake step *) }
     ; { label= "importing repository"
       ; message= "Importing your repository data..."
       ; duration= 0 (* stays here until page loads *) } |]

  let[@react.component] make () =
    let step_index, set_step_index = React.useState (fun () -> 0) in
    (* advance through steps on a timer *)
    React.useEffect1
      (fun () ->
        let current = steps.(step_index) in
        if current.duration > 0 && step_index < Array.length steps - 1 then
          let timer_id =
            Js.Global.setTimeout
              ~f:(fun () -> set_step_index (fun i -> i + 1))
              current.duration
          in
          Some (fun () -> Js.Global.clearTimeout timer_id)
        else None )
      [|step_index|] ;
    let current = steps.(step_index) in
    let step_number = step_index + 1 in
    <div>
      <div className="mb-6">
        <div className="flex items-center gap-1 text-sm text-mist-100 mb-2">
          <span>
            (string
               ( "step " ^ string_of_int step_number ^ " of "
               ^ string_of_int total_steps ^ ": " ) )
          </span>
          <span className="font-medium text-mana-100">
            (string current.label)
          </span>
        </div>
        <div
          className="w-full bg-mist-60 rounded-full h-3"
          style=(ReactDOM.Style.make
                   ~boxShadow:"0 4px 8px 0 rgba(115, 117, 121, 0.30) inset" () )>
          <div
            className="bg-mana-100 shadow-elixir h-3 rounded-full \
                       transition-all duration-500"
            style=(ReactDOM.Style.make
                     ~width:
                       (string_of_int (step_number * 100 / total_steps) ^ "%")
                     () )
          />
        </div>
      </div>
      <div className="flex flex-col items-center py-8">
        <div
          className="animate-spin w-8 h-8 border-2 border-mana-100 \
                     border-t-transparent rounded-full mb-4"
        />
        <p className="text-mist-100 text-center">(string current.message)</p>
      </div>
    </div>
end

module DidDisplay = struct
  let[@react.component] make ~did () =
    <p className="text-xs text-mist-80 mb-4">
      (string "Account DID: ")
      <code className="bg-mist-60/50 px-1 rounded">(string did)</code>
    </p>
end

module CredentialsForm = struct
  let[@react.component] make ~csrf_token ~invite_required () =
    let is_submitting, set_is_submitting = React.useState (fun () -> false) in
    let handle_submit _ = set_is_submitting (fun _ -> true) in
    if is_submitting then <LoadingOverlay />
    else
      array
        [| <p key="desc" className="w-full text-balance text-mist-100 mb-4">
             (string
                "Migrate your existing atproto account to this PDS. You'll \
                 need your account credentials from your current PDS." )
           </p>
         ; <form
             key="form"
             className="w-full flex flex-col gap-y-3"
             onSubmit=handle_submit>
             <input type_="hidden" name="dream.csrf" value=csrf_token />
             <input type_="hidden" name="action" value="start_migration" />
             ( if invite_required then
                 <Input
                   name="invite_code"
                   type_="text"
                   label="Invite code"
                   placeholder="a1b2c3d4"
                   required=true
                   showIndicator=false />
               else null )
             <Input
               name="identifier"
               type_="text"
               label="Existing handle or DID"
               placeholder="you.bsky.social or did:plc:..."
               required=true
               showIndicator=false
             />
             <Input
               name="password"
               type_="password"
               label="Existing password"
               required=true
               showIndicator=false
             />
             <p className="text-xs text-mist-80 -mt-1">
               (string
                  "Use your actual account password, not an app password. Your \
                   password is only used to log into your current PDS and is \
                   never stored." )
             </p>
             <Button type_="submit" formMethod="post" className="mt-2">
               (string "start migration")
             </Button>
           </form>
         ; <div key="footer" className="mt-4">
             <span className="text-sm text-mist-100">
               (string "Don't have an account? ")
               <a
                 href="/account/signup"
                 className="text-mana-100 underline hover:text-mana-200">
                 (string "sign up")
               </a>
               (string " instead.")
             </span>
           </div> |]
end

module ResumeForm = struct
  let[@react.component] make ~csrf_token ~did () =
    let is_submitting, set_is_submitting = React.useState (fun () -> false) in
    let handle_submit _ = set_is_submitting (fun _ -> true) in
    if is_submitting then <LoadingOverlay />
    else
      <div>
        <div
          className="bg-mana-100/10 border border-mana-100/30 rounded-lg p-4 \
                     mb-4">
          <div className="flex items-start gap-3">
            <CheckmarkIcon
              className="w-5 h-5 text-mana-100 mt-0.5 flex-shrink-0"
            />
            <div>
              <h3 className="font-medium text-mana-100 mb-1">
                (string "Previous migration found")
              </h3>
              <p className="text-sm text-mist-100">
                (string
                   "An incomplete migration exists for this account. Enter \
                    your credentials again to continue where you left off." )
              </p>
            </div>
          </div>
        </div>
        (match did with Some d -> <DidDisplay did=d /> | None -> null)
        <form className="w-full flex flex-col gap-y-3" onSubmit=handle_submit>
          <input type_="hidden" name="dream.csrf" value=csrf_token />
          <input type_="hidden" name="action" value="resume_migration" />
          <Input
            name="identifier"
            type_="text"
            label="Handle or DID"
            placeholder="you.bsky.social or did:plc:..."
            required=true
            showIndicator=false
          />
          <Input
            name="password"
            type_="password"
            label="Password"
            required=true
            showIndicator=false
          />
          <p className="text-xs text-mist-80 -mt-1">
            (string
               "Enter your credentials from your original PDS to resume the \
                migration." )
          </p>
          <Button type_="submit" formMethod="post" className="mt-2">
            (string "resume migration")
          </Button>
        </form>
        <div className="mt-4">
          <a
            href="/account/migrate"
            className="text-sm text-mist-100 underline hover:text-mana-100">
            (string "Start a new migration instead")
          </a>
        </div>
      </div>
end

module TwoFactorForm = struct
  let[@react.component] make ~csrf_token ~identifier ~old_pds ~invite_code () =
    let is_submitting, set_is_submitting = React.useState (fun () -> false) in
    let handle_submit _ = set_is_submitting (fun _ -> true) in
    if is_submitting then <LoadingOverlay />
    else
      <div>
        <p className="text-mist-100 mb-4">
          (string
             "Your account requires two-factor authentication. Check your \
              email for a sign-in code." )
        </p>
        <form className="w-full flex flex-col gap-y-3" onSubmit=handle_submit>
          <input type_="hidden" name="dream.csrf" value=csrf_token />
          <input type_="hidden" name="action" value="submit_2fa" />
          <input type_="hidden" name="identifier" value=identifier />
          <input type_="hidden" name="old_pds" value=old_pds />
          ( match invite_code with
          | Some code ->
              <input type_="hidden" name="invite_code" value=code />
          | None ->
              null )
          <Input
            name="password"
            type_="password"
            label="Password"
            required=true
            showIndicator=false
          />
          <Input
            name="auth_factor_token"
            type_="text"
            label="Authentication code"
            placeholder="123456"
            required=true
            showIndicator=false
          />
          <p className="text-xs text-mist-80 -mt-1">
            (string "Enter your password and the code from your email.")
          </p>
          <Button type_="submit" formMethod="post" className="mt-2">
            (string "continue")
          </Button>
        </form>
        <div className="mt-4">
          <a
            href="/account/migrate"
            className="text-sm text-mist-100 underline hover:text-mana-100">
            (string "Start over")
          </a>
        </div>
      </div>
end

module BlobProgress = struct
  let[@react.component] make ~csrf_token ~did ~blobs_imported ~blobs_failed () =
    <div>
      <div className="flex flex-col items-center py-6">
        <div
          className="animate-spin w-8 h-8 border-2 border-mana-100 \
                     border-t-transparent rounded-full mb-4"
        />
        <p className="text-mist-100 text-center mb-1">
          (string "Importing media...")
        </p>
        <p className="text-sm text-mist-80 text-center">
          (string (Printf.sprintf "%d blobs imported" blobs_imported))
          ( if blobs_failed > 0 then
              <span className="text-phoenix-100">
                (string (Printf.sprintf " (%d failed)" blobs_failed))
              </span>
            else null )
        </p>
      </div>
      <form className="flex justify-center">
        <input type_="hidden" name="dream.csrf" value=csrf_token />
        <input type_="hidden" name="action" value="continue_blobs" />
        ( match did with
        | Some d ->
            <input type_="hidden" name="did" value=d />
        | None ->
            null )
        <Button type_="submit" formMethod="post">
          (string "continue importing")
        </Button>
      </form>
    </div>
end

module PlcTokenForm = struct
  let[@react.component] make ~csrf_token ~did () =
    <div>
      <p className="text-mist-100 mb-4">
        (string
           "Your account has been created and your data has been imported. To \
            complete the migration, enter the confirmation code that was sent \
            to your email." )
      </p>
      <form className="w-full flex flex-col gap-y-3">
        <input type_="hidden" name="dream.csrf" value=csrf_token />
        <input type_="hidden" name="action" value="submit_plc_token" />
        <Input
          name="plc_token"
          type_="text"
          label="Confirmation code"
          placeholder="A1B2C-3D4E5"
          required=true
          showIndicator=false
        />
        <p className="text-xs text-mist-80 -mt-1">
          (string
             "This code was sent to the email address belonging to your \
              account on your old PDS." )
        </p>
        <Button type_="submit" formMethod="post" className="mt-2">
          (string "complete migration")
        </Button>
      </form>
      <form className="mt-3">
        <input type_="hidden" name="dream.csrf" value=csrf_token />
        <input type_="hidden" name="action" value="resend_plc_token" />
        <button
          type_="submit"
          formMethod="post"
          className="text-sm text-mist-100 underline hover:text-mana-100">
          (string "Resend confirmation code")
        </button>
      </form>
    </div>
end

module CompleteView = struct
  let[@react.component] make ~handle ~blobs_failed ~old_account_deactivated
      ~old_account_deactivation_error () =
    <div className="py-4">
      <div className="flex items-center gap-3 mb-4">
        <div
          className="w-10 h-10 bg-mana-100/20 rounded-full flex items-center \
                     justify-center">
          <CheckmarkIcon className="w-5 h-5 text-mana-100" />
        </div>
        <div>
          <h2 className="text-lg font-serif text-mana-100">
            (string "Migration complete!")
          </h2>
          <p className="text-sm text-mist-100">
            (string "Your account")
            ( match handle with
            | Some h ->
                <span className="font-medium text-mana-100">
                  (string (" @" ^ h))
                </span>
            | None ->
                null )
            (string " is now on this PDS.")
          </p>
        </div>
      </div>
      ( if old_account_deactivated then
          <p className="mb-4 text-sm text-mist-80">
            (string
               "Your account on your old PDS has been deactivated \
                automatically." )
          </p>
        else
          match old_account_deactivation_error with
          | Some err ->
              <div
                className="mb-4 p-3 bg-phoenix-100/10 border \
                           border-phoenix-100/30 rounded-lg">
                <div className="flex items-start gap-2">
                  <CircleAlertIcon
                    className="w-4 h-4 text-phoenix-100 mt-0.5 flex-shrink-0"
                  />
                  <div>
                    <p className="text-sm text-phoenix-100 font-medium">
                      (string "Could not deactivate your old account")
                    </p>
                    <p className="text-xs text-phoenix-100/80 mt-1">
                      (string err)
                    </p>
                    <p className="text-sm text-mist-100 mt-2">
                      (string
                         "You may need to log into your account on your old \
                          PDS to deactivate it there." )
                    </p>
                  </div>
                </div>
              </div>
          | None ->
              <p className="mb-4 text-sm text-mist-80">
                (string
                   "Make sure to log into your account on your old PDS to \
                    deactivate it there." )
              </p> )
      ( if blobs_failed > 0 then
          <div
            className="mb-4 p-3 bg-phoenix-100/10 border border-phoenix-100/30 \
                       rounded-lg">
            <p className="text-sm text-phoenix-100">
              (string
                 (Printf.sprintf
                    "%d blob(s) failed to import. This means some of your \
                     content may appear to have missing media."
                    blobs_failed ) )
            </p>
          </div>
        else null )
      <div className="flex gap-3">
        <a href="/account"><Button>(string "go to account")</Button></a>
      </div>
    </div>
end

module ErrorView = struct
  let[@react.component] make () =
    <div>
      <p className="text-mist-100 mb-2">
        (string
           "Something went wrong during migration. Don't worry, your data on \
            your original PDS is safe." )
      </p>
      <p className="text-mist-100 mb-4">
        (string
           "Check your internet connection and try again, or contact the PDS \
            administrator if the problem persists.\n\n" )
      </p>
      <div className="flex gap-3">
        <a href="/account/migrate"><Button>(string "try again")</Button></a>
        <a href="/account/login">
          <Button kind=`Secondary>(string "back to login")</Button>
        </a>
      </div>
    </div>
end

type props =
  { csrf_token: string
  ; invite_required: bool
  ; hostname: string
  ; step: string [@default "enter_credentials"]
  ; did: string option [@default None]
  ; handle: string option [@default None]
  ; old_pds: string option [@default None]
  ; identifier: string option [@default None]
  ; invite_code: string option [@default None]
  ; blobs_imported: int [@default 0]
  ; blobs_failed: int [@default 0]
  ; old_account_deactivated: bool [@default false]
  ; old_account_deactivation_error: string option [@default None]
  ; error: string option [@default None]
  ; message: string option [@default None] }
[@@deriving json]

let[@react.component] make
    ~props:
      ({ csrf_token
       ; invite_required
       ; hostname= _
       ; step
       ; did
       ; handle
       ; old_pds
       ; identifier
       ; invite_code
       ; blobs_imported
       ; blobs_failed
       ; old_account_deactivated
       ; old_account_deactivation_error
       ; error
       ; message } :
        props ) () =
  let current_step = step_of_string step in
  let step_number = step_to_number current_step in
  let show_progress =
    current_step <> EnterCredentials
    && current_step <> ResumeAvailable
    && current_step <> Enter2FA && current_step <> Error
    && current_step <> Complete
  in
  <main className="w-full h-auto max-w-md px-4 sm:px-0 my-auto">
    <h1 className="text-2xl font-serif text-mana-200 mb-2">
      (string "migrate account")
    </h1>
    ( if show_progress then
        <ProgressIndicator step_number label=(step_to_label current_step) />
      else null )
    ( match error with
    | Some err ->
        <AlertMessage message=err kind=`Error />
    | None ->
        null )
    ( match message with
    | Some msg when current_step <> Complete ->
        <AlertMessage message=msg kind=`Success />
    | _ ->
        null )
    ( match current_step with
    | EnterCredentials ->
        <CredentialsForm csrf_token invite_required />
    | ResumeAvailable ->
        <ResumeForm csrf_token did />
    | Enter2FA ->
        <TwoFactorForm
          csrf_token
          identifier=(Option.value ~default:"" identifier)
          old_pds=(Option.value ~default:"" old_pds)
          invite_code
        />
    | ImportingData ->
        <BlobProgress csrf_token did blobs_imported blobs_failed />
    | EnterPlcToken ->
        <PlcTokenForm csrf_token did />
    | Complete ->
        <CompleteView
          handle
          blobs_failed
          old_account_deactivated
          old_account_deactivation_error
        />
    | Error ->
        <ErrorView /> )
  </main>
