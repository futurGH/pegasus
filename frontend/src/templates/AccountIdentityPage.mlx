[@@@ocaml.warning "-26-27"]

open Melange_json.Primitives
open React

type actor = AccountSwitcher.actor =
  {did: string; handle: string; avatar_data_uri: string option [@default None]}
[@@deriving json]

type props =
  { current_user: actor
  ; logged_in_users: actor list
  ; csrf_token: string
  ; is_plc: bool
  ; credentials_json: string option [@default None]
  ; error: string option [@default None]
  ; success: string option [@default None] }
[@@deriving json]

let[@react.component] make
    ~props:
      ({ current_user
       ; logged_in_users
       ; csrf_token
       ; is_plc
       ; credentials_json
       ; error
       ; success } :
        props ) () =
  let credentialsPlaceholder = Option.value credentials_json ~default:"{}" in
  let credentialsInput, setCredentialsInput =
    useState (fun () -> credentialsPlaceholder)
  in
  let loading, setLoading = useState (fun () -> false) in
  let errorState, setErrorState = useState (fun () -> error) in
  let successState, setSuccessState = useState (fun () -> success) in
  <div
    className="w-full h-full max-w-[816px] px-8 pt-16 mx-auto flex flex-col \
               md:flex-row gap-8">
    <AccountSidebar
      current_user logged_in_users active_page="/account/identity"
    />
    <main className="flex-1 w-full md:max-w-lg">
      <h1 className="text-2xl font-serif text-mana-200 mb-1">
        (string "identity")
      </h1>
      ( if not is_plc then
          <p className="text-mist-100">
            (string "Identity management is only available for did:plc accounts.")
          </p>
        else
          <div>
            <p className="text-mist-100 mb-4">
              (string
                 "Managing your PLC identity could render your account \
                  permanently unusable. Make sure you know what you're doing!" )
            </p>
            <ClientOnly
              fallback=(
                <div className="flex flex-col gap-y-4">
                  <textarea
                    className="w-full h-96 p-3 font-mono text-sm bg-feather-100 \
                               border border-mist-60 rounded-xl text-mana-200 \
                               resize-none focus:outline-none focus:border-mana-100"
                    value=credentialsPlaceholder
                    disabled=true
                  />
                  <Button type_="submit" disabled=true>
                    (string "submit operation")
                  </Button>
                </div>
              )>
              [%browser_only
                (fun () ->
                    let submitOperation () =
                      setLoading (fun _ -> true) ;
                      setErrorState (fun _ -> None) ;
                      setSuccessState (fun _ -> None) ;
                      let body =
                        Fetch.BodyInit.make
                          (Webapi.Url.URLSearchParams.makeWithArray
                             [| ("dream.csrf", csrf_token)
                              ; ("action", "submit")
                              ; ("credentials", credentialsInput) |]
                          |> Webapi.Url.URLSearchParams.toString )
                      in
                      let _ =
                        Fetch.fetchWithInit "/account/identity"
                          (Fetch.RequestInit.make ~method_:Post ~body
                             ~headers:
                               (Fetch.HeadersInit.makeWithArray
                                  [| ( "Content-Type"
                                     , "application/x-www-form-urlencoded" ) |] )
                             () )
                        |> Js.Promise.then_ (fun response ->
                               setLoading (fun _ -> false) ;
                               Fetch.Response.json response )
                        |> Js.Promise.then_ (fun json ->
                               let open Js.Json in
                               let dict =
                                 decodeObject json |> Option.value ~default:(Js.Dict.empty ())
                               in
                               ( match Js.Dict.get dict "error" with
                               | Some err_json ->
                                   let err =
                                     decodeString err_json
                                     |> Option.value ~default:"An error occurred"
                                   in
                                   setErrorState (fun _ -> Some err)
                               | None ->
                                   setSuccessState (fun _ ->
                                       Some "Operation submitted successfully." ) ) ;
                               Js.Promise.resolve () )
                        |> Js.Promise.catch (fun _ ->
                               setLoading (fun _ -> false) ;
                               setErrorState (fun _ ->
                                   Some "An error occurred. Please try again." ) ;
                               Js.Promise.resolve () )
                      in
                      ()
                    in
                    <div className="flex flex-col gap-y-4">
                      <textarea
                        className="w-full h-96 p-3 font-mono text-sm bg-feather-100 \
                                   border border-mist-60 rounded-xl text-mana-200 \
                                   resize-none focus:outline-none focus:border-mana-100"
                        value=credentialsInput
                        onChange=(fun e ->
                            setCredentialsInput (fun _ ->
                                (Event.Form.target e)##value ) )
                        disabled=loading
                      />
                      ( match errorState with
                      | Some err ->
                          <span
                            className="inline-flex items-center text-phoenix-100 \
                                       text-sm">
                            <CircleAlertIcon className="w-4 h-4 mr-2" />
                            (string err)
                          </span>
                      | None ->
                          null )
                      ( match successState with
                      | Some msg ->
                          <span
                            className="inline-flex items-center text-mana-100 \
                                       text-sm">
                            (string msg)
                          </span>
                      | None ->
                          null )
                      <Button
                        type_="button"
                        disabled=loading
                        onClick=(fun _ -> submitOperation ())>
                        (string
                           (if loading then "submitting..." else "submit operation") )
                      </Button>
                    </div> )]
            </ClientOnly>
          </div> )
    </main>
  </div>
