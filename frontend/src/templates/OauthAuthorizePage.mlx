open Melange_json.Primitives
open React

let cimd_suffix_len = String.length "/oauth-client-metadata.json"

type props =
  { client_url: string * string (* (host, path) *)
  ; client_name: string option [@default None]
  ; handle: string
  ; scopes: string list
  ; code: string
  ; request_uri: string
  ; csrf_token: string }
[@@deriving json]

let[@react.component] make
    ~props:
      ({client_url; client_name; handle; scopes; code; request_uri; csrf_token} :
        props ) () =
  let host, path = client_url in
  let rendered_name =
    match client_name with
    | Some client_name ->
        <span className="text-mana-100 font-serif">
          (string client_name)
          <span className="font-sans">(string (" (" ^ host ^ ")"))</span>
        </span>
    | None when String.length path = 0 ->
        <span className="text-mana-100 font-serif">(string host)</span>
    | None ->
        <span className="text-mana-100 font-serif">
          (string host) <span className="text-mana-40">(string path)</span>
        </span>
  in
  let rendered_handle =
    <span className="text-mana-100 font-serif">(string @@ "@" ^ handle)</span>
  in
  <main className="w-full h-auto max-w-lg px-4 sm:px-0">
    <h1 className="text-2xl font-serif text-mana-200 mb-2">
      (string ("authorizing " ^ host))
    </h1>
    <p className="w-full text-mist-100">
      (string "You're signing into ")
      rendered_name
      (string " as ")
      rendered_handle
      (string " and granting it the following permissions:")
    </p>
    <ul className="w-full text-mist-100 list-disc ml-8 mt-2 space-y-1">
      ( List.map (fun scope -> <li>(string scope)</li>) scopes
      |> Array.of_list |> array )
    </ul>
    <form className="w-full flex flex-row items-center justify-between mt-6">
      <input type_="hidden" name="dream.csrf" value=csrf_token />
      <input type_="hidden" name="code" value=code />
      <input type_="hidden" name="request_uri" value=request_uri />
      <Button
        kind=`Secondary
        type_="submit"
        formMethod="post"
        name="action"
        value="deny"
        className="grow basis-1/3 min-w-0">
        (string "cancel")
      </Button>
      <Button
        kind=`Primary
        type_="submit"
        formMethod="post"
        name="action"
        value="allow"
        className="grow basis-2/3 min-w-0 max-w-2xs">
        (string "authorize")
      </Button>
    </form>
  </main>
